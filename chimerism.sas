/* Macro for assisting chimerism review by Scientific Director
/* Author: Hai-Lin Wang <hwang@mcw.edu>; Kyle Hebert <khebert@mcw.edu>
/*
/* Purpose:
/* -------------------------------------------- */
/* 1) Identify chimerism results via pre-determined logic
/*
/* Retrieval restriction:
/* -------------------------------------------- */
/* TED or CRF, also require RDB CHIMER table (vertical dataset) for reference
/* 
/* Additional Documents Required: None
/* -------------------------------------------- */
/*
/* Macro function and parameters
/* -------------------------------------------- */
/* %chimreview()
/*    - &indata: specify input dataset
/*    - &rdbacnt / &rdbpswd: if SAS program has direct access to RDB (for MKE user), 
/*      then specify your RDB account and password
/*    - &inchimdata: if SAS program has no direct access to RDB (for MSP user), 
/*      then specify separately pulled RDB CHIMER table / dataset here, 
/*      must include following variables: crid txnum chcellty chimmeth chimdate dnrpct
/*    - &outchimdata: provide chimerism vertical dataset with computed results where applicable 
/*      to assist Scientific Director review of chimerism results
/*
/* Required Variables:
/* -------------------------------------------- */
/* From retrieval: crid txnum 
/* From CHIMER table: crid txnum chcellty chimmeth chimdate dnrpct
/*
/* Variables Generated by macro function %chimreview():
/* -------------------------------------------- */
/* chimrev chimrevtag
/*
/* Notes:
/* -------------------------------------------- */
/* 1) Primary intention of %chimreview() macro is to assist instead of replace Scientifc Director review
/*    Following logics are used to determine a chimerism result:
/*      rule 1: minimum value of dnrpct is >=95 -> full chimerism
/*      rule 2: the maximum value of dnrpct <80 and mininum value is >25 -> mixed chimerism
/*      rule 3: the maximum value of dnrpct <5 -> graft failure
/*      rule 4: same cell type, starting from 80 or above, always increase (or stay the same) in subsequent detection until 95 or higher -> full chimerism
/*      rule 5: same cell type, always decrease (or stay the same) in subsequent detection until <5 -> graft failure
/* 2) Valid RDB account and password required for %chimreview() macro to function
/* -------------------------------------------- */

proc format library = export;
  value chimmeth
  1='Karyotyping'
  2='FISH'
  3='RFLP'
  4='PCR'
  5='HLA typing'
  6='VNTR or STR'
  7='Other'
  8='ABO blood group change'
  90='Other'
  88='Unknown'
  99='Not answered';
  value chimceltp
  1='BM'
  2='PBMC'
  3='T-cells'
  4='B-cells'
  5='Red cells'
  6='Monocytes'
  7='PMNs (Neutrophils)'
  8='Lymphocytes'
  9='Myeloid cells'
  90='Other'
  88='Unknown'
  99='Not answered'
  10='Unsoretd/whole'
  11='CD34+ cells'
  12='Total mononuclear cells'
  13='Granulocytes'
  14='NK cells(CD56+)';
  value chimrev 
  1='1 Full chimerism' 
  2='2 Mixed chimerism' 
  3='3 Graft failure'
  88='TBD by Sci Dir';
  value chimrevtag
  1='(FC) Donor minimum >=95%' 
  2='(MC) Donor mininum >25% and maximum <80%' 
  3='(GF) Donor maximum <5%' 
  4='(FC) Donor starting from >=80% and always increasing for same cell type' 
  5='(GF) Donor always decreasing and end up <5% for same cell type'
  88='TBD by Sci Dir';

%macro chimreview(indata = , rdbacnt = , rdbpswd = , inchimdata = , outchimdata =);

%if &inchimdata NE %then %do;
  data chimtmp (compress=binary); set &inchimdata; run; 
%end;
%else %if &rdbacnt NE and &rdbpswd NE %then %do;
  proc sql;
  connect to oracle ( user="&rdbacnt" orapw="&rdbpswd" path="(DESCRIPTION = (ADDRESS_LIST = (ADDRESS = (PROTOCOL = TCP)(HOST = crdbtest.cibmtr.mcw.edu)(PORT = 1521)))(CONNECT_DATA = (SID = IBMTTEST)))");
  create table chimtmp (compress=binary) as 
  select a.crid, a.txnum, 
         datepart(b.chimdate) as chimdate format=mmddyy10. label='Test date', 
         b.chcellty format=chimceltp. label='Cell type', 
         b.chimmeth format=chimmeth. label='Method',
         b.dnrpct label='Donor %'
  from &indata as a 
  left join (select * from connection to oracle (select crid,txnum,chcellty,chimmeth,chimdate,dnrpct from cibmtr.chimer)) as b on a.crid=b.crid and a.txnum=b.txnum
  order by crid,txnum;
  disconnect from oracle;
  quit;  
%end;
%else %do;
  %put ERROR: Must provide chimerism data either through "inchimdata" parameter or RDB account info;
  %abort;
%end;

proc sort data=chimtmp; by crid txnum chcellty chimdate; run;
/*Create instance number of chimerism percentage by date of report for each CRID, TXNUM, cell type*/
data chimtmp2 (compress=binary); 
  retain chinum; 
  set chimtmp; 
  by crid txnum chcellty chimdate; 
  chinum+1; 
  if first.chcellty then chinum=1;
run;

/*Create donor percent change comparing to last instance*/
proc sql; create table chimtmp3 (compress=binary) as
select a.*,(a.dnrpct-b.dnrpct) as dnrpctchange
from chimtmp2 as a left join chimtmp2 as b
on a.crid=b.crid and a.txnum=b.txnum and a.chcellty=b.chcellty and a.chinum-1=b.chinum;

/*Evaluate rule 1,2,3 for each HCT*/
proc sql; create table ruleset1 (compress=binary) as
select crid, txnum, min(dnrpct) as dnrpct_min, max(dnrpct) as dnrpct_max, 
      (min(dnrpct)>=95) as rule1_fc, (0<=max(dnrpct)<80 and min(dnrpct)>25) as rule2_mc, (0<=max(dnrpct)<5) as rule3_gf
from chimtmp3 group by crid,txnum;

/*Evaluate rule 4,5 for each HCT*/
proc sql; create table ruleset2 (compress=binary) as
select crid, txnum, max(rule4_byct_fc) as rule4_fc, max(rule5_byct_gf) as rule5_gf from (
    select crid, txnum, chcellty,
           (min(dnrpct)>80 and max(dnrpct)>=95 and min(dnrpctchange)>=0) as rule4_byct_fc,
           (0<=min(dnrpct)<5 and .<max(dnrpctchange)<=0 ) as rule5_byct_gf
    from chimtmp3 group by crid,txnum,chcellty)
group by crid,txnum;

/*Create HCT level result*/
proc sql; create table final (compress=binary) as
select study.*,
       a.rule1_fc, a.rule2_mc, a.rule3_gf, b.rule4_fc, b.rule5_gf,
       case when a.rule3_gf then 3 when b.rule5_gf then 5 when a.rule2_mc then 2 when a.rule1_fc then 1 when b.rule4_fc then 4 else 88 end as chimrevtag format=chimrevtag.,
       case when a.rule3_gf or b.rule5_gf then 3 when a.rule2_mc then 2 when a.rule1_fc or b.rule4_fc then 1 else 88 end as chimrev format=chimrev.
from &indata as study 
left join ruleset1 as a on study.crid=a.crid and study.txnum=a.txnum
left join ruleset2 as b on study.crid=b.crid and study.txnum=b.txnum
order by crid,txnum;

data &indata (compress=binary); set final; run;

/*Create vertical chimerism level data, with computed results to assist Sci Dir review*/
proc sql; create table &outchimdata (compress=binary) as
select a.*,b.datetx,b.chimrev, b.chimrevtag
from chimtmp as a left join final as b
on a.crid=b.crid and a.txnum=b.txnum
order by crid,txnum,chimdate,chcellty;

ods excel close;
ods excel file = "Chimerism review.xlsx" style = kenyxls
options(frozen_rowheaders = "2" frozen_headers = "1");
proc report data=&outchimdata;
  column crid datetx chimdate chcellty dnrpct chimrev chimrevtag;
  define crid / group 'CRID';
  define datetx / group 'HCT date' format=mmddyy10.;
  define chimdate / display 'Chim. test date' format=mmddyy10.;
  define chcellty / display 'Cell type';
  define dnrpct / display 'Donor %';
  define chimrev / group 'Result';
  define chimrevtag / group 'Rationale' style(column)=[cellwidth=5in];
  compute before datetx;
      i + 1;
  endcomp;
  compute datetx;
      if mod(i,2) eq 1
         then call define(_row_, "style", "STYLE=[background=lightgrey]");
  endcomp;
run;
ods excel close;
%mend;



